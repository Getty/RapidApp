package RapidApp::ErrorView;

use Moose;
extends 'RapidApp::AppStoreForm2';

use RapidApp::Include qw(perlutil sugar);

# make sure the as_html method gets loaded into StackTrace, which might get deserialized
use Devel::StackTrace;
use Devel::StackTrace::WithLexicals;
use Devel::StackTrace::AsHTML;

has 'exceptionStore' => ( is => 'rw' ); # either a store object, or a Model name

sub BUILD {
	my $self= shift;
	
	# Register ourselves with RapidApp if no other has already been registered
	# This affects any hyperlinks to exception reports generated by RapidApp modules.
	defined $self->app->rapidApp->errorViewPath
		or $self->app->rapidApp->errorViewPath($self->base_url);
	
	$self->apply_actions(
		view => 'view',
		die => 'gen_die',
		error => 'gen_error',
		usererror => 'gen_usererror',
	);
	
	$self->auto_web1(1);
}

sub getExceptionObject {
	my ($self, $id)= @_;
	# Generating an exception while trying to view exceptions wouldn't be too useful
	#   so we trap and display exceptions specially in this module.
	my $err;
	try {
		defined $id or die "No ID specified";
		
		my $store= $self->exceptionStore;
		defined $store or die "No ExceptionStore configured";
		ref $store or $store= $self->c->model($store);
		
		$err= $store->loadException($id);
	}
	catch {
		$err= { id => $id, error => $_ };
	};
	return $err;
}

sub extconfig {
	my $self= shift;
	my $id= $self->c->req->params->{id};
	defined $id or die "No ID specified";
	
	my $err= $self->getExceptionObject($id);
	
	return {
		xtype => 'box',
		html => $self->c->view("RapidApp::TT")->render($self->c, 'templates/rapidapp/exception.tt', { ex => $err })
	};
}

sub view {
	my $self= shift;
	my $id= $self->c->req->params->{id};
	defined $id or die "No ID specified";
	
	my $err= $self->getExceptionObject($id);
	
	$self->c->stash->{ex}= $err;
	$self->c->stash->{current_view}= 'RapidApp::TT';
	$self->c->stash->{template}= 'templates/rapidapp/exception.tt';
}

sub gen_die {
	die "Deliberately generating an exception";
}

sub gen_error {
	die RapidApp::Error->new("Generating an exception using the RapidApp::Error class");
}

sub gen_usererror {
	die usererr "PEBKAC";
}

1;
